rootProject.ext.dependencyCustom = { moduleName ->
    def moduleConfig = rootProject.ext.modulePkgConfig[moduleName]
    if (moduleConfig) {
        if (moduleConfig.libDepend) {
            return files("${rootDir}/libs/${moduleConfig.libArtifactId}-${moduleConfig.libVersion}.aar")
        } else {
            return project(":${moduleName}")
        }
    } else {
        return project(":${moduleName}")
    }
}

afterEvaluate {
    def moduleConfig = rootProject.ext.modulePkgConfig[project.name]
    if (moduleConfig != null) {
        def arrFileName = "${moduleConfig.libArtifactId}-${moduleConfig.libVersion}.aar"
        publishing {
            project.android.libraryVariants.all { variant ->
                def buildType = variant.buildType.name
                if (buildType == 'release') {
                    def flavorName = variant.flavorName
                    def publicationName = "${project.name}${flavorName}${buildType}"
                    def componentName = "${flavorName}Release"
                    if (flavorName == null || flavorName == "") {
                        componentName = "release"
                    }
                    def component = components.findByName(componentName)
                    if (component != null) {
                        publishing.publications.create(publicationName, MavenPublication) {
                            from component
                            groupId = "com.pine.libs"
                            artifactId = "${moduleConfig.libArtifactId}-${flavorName}"
                            version = moduleConfig.libVersion
                        }
                    }
                }
            }

            repositories {
                maven {
                    url "${rootDir}/maven"
                }
            }
        }
    }
}