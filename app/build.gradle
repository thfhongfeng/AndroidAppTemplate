plugins {
    id 'com.android.application'
    id 'maven-publish'
}

android {
    compileSdk build_versions.compileSdkVersion

    defaultConfig {
        minSdk build_versions.minSdkVersion
        targetSdk build_versions.targetSdkVersion

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        //必须要保证所有的flavor 都属于同一个维度
        flavorDimensions "default"
    }

    buildFeatures {
        dataBinding = true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    productFlavors {
        common {
            applicationId build_product_applicationId.common
            versionCode build_product_versionCode.common
            versionName build_product_versionName.common
        }
        special {
            applicationId build_product_applicationId.special
            versionCode build_product_versionCode.special
            versionName build_product_versionName.special
        }
    }
    signingConfigs {
        common {
            storeFile file(build_product_storeFile.common)
            storePassword build_product_storePassword.common
            keyAlias build_product_keyAlias.common
            keyPassword build_product_keyPassword.common
        }
        special {
            storeFile file(build_product_storeFile.special)
            storePassword build_product_storePassword.special
            keyAlias build_product_keyAlias.special
            keyPassword build_product_keyPassword.special
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            productFlavors.common.signingConfig signingConfigs.common
            productFlavors.special.signingConfig signingConfigs.special
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            minifyEnabled false
            initWith(buildTypes.release)
            // 使用了initWith，需要在之后覆盖debug特有的属性
            debuggable true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    applicationVariants.all { variant ->
        def buildTypeName = variant.buildType.name
        String timeTag = ""
        if (!"debug".equalsIgnoreCase(buildTypeName)) {
            timeTag = "-${releaseTime()}"
        }
        variant.outputs.all {
            outputFileName = "${build_versions.apkHeadName}-${variant.flavorName}-${variant.versionCode}${timeTag}.apk"
        }
    }
}

static def releaseTime() {
    return new Date().format("yyyyMMddHHmm", TimeZone.getTimeZone("GMT+8"))
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    // 入口模块 begin
    implementation project(':app_welcome')
    // 入口模块 end

    // bundle模块 begin
    implementation project(':bundle_login')
    implementation project(':bundle_main')
    implementation project(':bundle_user')

    implementation project(':db_server')
    implementation project(':bundle_business_mvc')
    implementation project(':bundle_business_mvp')
    implementation project(':bundle_business_mvvm')
    // bundle模块 end
}

afterEvaluate {
    publishing {
        publications {
            commonRelease(MavenPublication) {
                from components.commonRelease_apk
                groupId = build_product_groupId.common
                artifactId = build_product_appName.common
                version = build_product_versionName.common
            }
            specialRelease(MavenPublication) {
                from components.specialRelease_apk
                groupId = build_product_groupId.special
                artifactId = build_product_appName.special
                version = build_product_versionName.special
            }
        }

        repositories {
            maven {
                url "file:///D:/MinicreateProducts"
            }
        }
    }
}